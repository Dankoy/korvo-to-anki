plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '8.1.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'ru.dankoy'
version = '0.3.9-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

repositories {
    mavenCentral()
}

ext {
    springShellVersion = '3.4.1'
    resilience4jBomVersion = '2.3.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
        mavenBom "io.github.resilience4j:resilience4j-bom:${resilience4jBomVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.shell:spring-shell-starter'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test'
    
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.3.2'

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    implementation 'ch.qos.logback:logback-classic'
    implementation 'ch.qos.logback:logback-core'

    // sqlite with default version from spring boot bom (3.43.2.0) doesn't work with spring boot 3.2.2
    // sqlite version 3.42.1 works with spring boot 3.1.3
    // Working sqlite version with spring boot 3.2.2 is 3.45.1.0
    implementation 'org.xerial:sqlite-jdbc:3.51.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // okhttp is not a part of spring dependencies BOM. Migrating to WebClient
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    // spotless
    implementation 'com.diffplug.spotless:spotless-lib:4.1.0'

    // resilence4j
    implementation 'io.github.resilience4j:resilience4j-spring-boot3'

    // mockito as javaagent
    testImplementation(libs.mockito)
     mockitoAgent(libs.mockito) {
         transitive = false
     }
}

tasks {
     test {
         jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
     }
 }

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

spotless {
    sql {
        target 'src/main/resources/**/*.sql' // have to set manually
        target 'src/test/resources/**/*.sql' // have to set manually

        dbeaver()  // has its own section below
//        prettier(['prettier': '3.0.3', 'prettier-plugin-sql': '0.18.0'])
        // has its own section below
    }
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        googleJavaFormat('1.25.1')
                .reflowLongStrings()
                .formatJavadoc(true)
                .reorderImports(false)
                .groupArtifact('com.google.googlejavaformat:google-java-format')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }

    yaml {
        target '.github/**/*.yaml', '.github/**/*.yml'
        jackson()
        prettier()
    }
}
