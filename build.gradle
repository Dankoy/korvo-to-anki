plugins {
    id 'java'
    alias(libs.plugins.spring.boot.plugin)
    alias(libs.plugins.spring.dependency.management.plugin)
    alias(libs.plugins.spotless.plugin)
}

group = 'ru.dankoy'
version = '0.3.9-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

repositories {
    mavenCentral()
}

ext {
    springShellVersion = libs.versions.spring.shell.get()
    resilience4jBomVersion = libs.versions.resilience4j.bom.get()
    googleJavaFormatVersion = libs.versions.google.java.format.get()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
        mavenBom "io.github.resilience4j:resilience4j-bom:${resilience4jBomVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-jackson'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.shell:spring-shell-starter-jansi'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test'
    
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation libs.okhttp3.mockwebserver

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // sqlite with default version from spring boot bom (3.43.2.0) doesn't work with spring boot 3.2.2
    // sqlite version 3.42.1 works with spring boot 3.1.3
    // Working sqlite version with spring boot 3.2.2 is 3.45.1.0
    implementation libs.sqlite.jdbc

    // okhttp is not a part of spring dependencies BOM. Migrating to WebClient
    implementation libs.okhttp3
    implementation libs.okhttp3.logging.interceptor

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    // spotless
    implementation libs.spotless.lib

    // resilence4j
    implementation 'io.github.resilience4j:resilience4j-spring-boot3'
    implementation 'org.springframework.boot:spring-boot-starter-aspectj' // necessary for resilience4j

    // mockito as javaagent
    testImplementation(libs.mockito)
     mockitoAgent(libs.mockito) {
         transitive = false
     }
}

tasks {
     test {
         jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
     }
 }

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

spotless {
    sql {
        target 'src/main/resources/**/*.sql' // have to set manually
        target 'src/test/resources/**/*.sql' // have to set manually

        dbeaver()  // has its own section below
//        prettier(['prettier': '3.0.3', 'prettier-plugin-sql': '0.18.0'])
        // has its own section below
    }
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        googleJavaFormat("${googleJavaFormatVersion}")
                .reflowLongStrings()
                .formatJavadoc(true)
                .reorderImports(false)
                .groupArtifact('com.google.googlejavaformat:google-java-format')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }

    yaml {
        target '.github/**/*.yaml', '.github/**/*.yml'
        jackson()
        prettier()
    }
}